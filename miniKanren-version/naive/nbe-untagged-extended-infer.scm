(load "../faster-miniKanren/mk-vicare.scm")
(load "../faster-miniKanren/mk.scm")
(load "../faster-miniKanren/test-check.scm")

(define lookupo
  (lambda (x env val)
    (fresh (y v env^)
      (== `((,y . ,v) . ,env^) env)
      (symbolo x)
      (symbolo y)
      (conde
        ((== x y) (== v val))
        ((=/= x y)
         (lookupo x env^ val))))))

(define !-o
  (lambda (gamma expr type)
    (conde
      ((== #f expr) (== 'Bool type))
      ((== #t expr) (== 'Bool type))
      ((numbero expr) (== 'Nat type))
      ((fresh (datum)
         (== `(quote ,datum) expr)
         (absento 'closure datum)
         (absento 'N datum)
         (!-quoted-datumo datum type)))
      ((symbolo expr) (lookupo expr gamma type))
      ((fresh (e)
         (== `(Ann ,e ,type) expr)
         (checko gamma e type)))
      ((fresh (e t)
         (== `(null? ,e) expr)
         (== 'Bool type)
         (!-o gamma e t)))
      ((fresh (e t)
         (== `(pair? ,e) expr)
         (== 'Bool type)
         (!-o gamma e t)))
      ((fresh (e t)
         (== `(number? ,e) expr)
         (== 'Bool type)
         (!-o gamma e t)))
      ((fresh (e t)
         (== `(symbol? ,e) expr)
         (== 'Bool type)
         (!-o gamma e t)))
      ((fresh (e)
         (== `(car ,e) expr)
         (!-o gamma e `(List ,type))))
      ((fresh (e a)
         (== `(cdr ,e) expr)
         (== `(List ,a) type)
         (!-o gamma e `(List ,a))))
      ((fresh (e1 e2 a)
         (== `(cons ,e1 ,e2) expr)
         (== `(List ,a) type)
         (!-o gamma e2 `(List ,a))
         (!-o gamma e1 a)))
      ((fresh (e1 e2 e3)
         (== `(if ,e1 ,e2 ,e3) expr)
         (checko gamma e1 'Bool)
         (!-o gamma e2 type)
         (checko gamma e3 type)))
      ((fresh (e1 e2 t1)
         (== `(,e1 ,e2) expr)
         (!-o gamma e1 `(-> ,t1 ,type))
         (checko gamma e2 t1))))))

(define !-quoted-datumo
  (lambda (datum type)
    (conde
      ((== #f datum) (== 'Bool type))
      ((== #t datum) (== 'Bool type))
      ((numbero datum) (== 'Nat type))
      ((symbolo datum) (== 'Sym type))
      ((== '() datum)
       (fresh (a)
         (== `(List ,a) type)))
      ((fresh (v1 v2 a)
         (== `(,v1 . ,v2) datum)
         (== `(List ,a) type)
         (!-quoted-datumo v2 `(List ,a))
         (!-quoted-datumo v1 a))))))

(define checko
  (lambda (gamma expr type)
    (conde
      ((fresh (x body t1 t2)
         (== `(lambda (,x) ,body) expr)
         (== `(-> ,t1 ,t2) type)
         (symbolo x)
         (checko `((,x . ,t1) . ,gamma) body t2)))
      ((!-o gamma expr type)))))


(define eval-expro
  (lambda (expr env val)
    (conde
      ((== #f expr) (== #f val))
      ((== #t expr) (== #t val))
      ((numbero expr) (== expr val))
      ((== `(quote ,val) expr)
       (absento 'closure val)
       (absento 'N val))
      ((fresh (x body)
         (== `(lambda (,x) ,body) expr)
         (== `(closure (,x) ,body ,env) val)
         (symbolo x)))
      ((symbolo expr) (lookupo expr env val))
      ((fresh (e v)
         (== `(null? ,e) expr)
         (eval-expro e env v)
         (eval-nullo v val)))
      ((fresh (e v)
         (== `(pair? ,e) expr)
         (eval-expro e env v)
         (eval-pairo v val)))
      ((fresh (e v)
         (== `(number? ,e) expr)
         (eval-expro e env v)
         (eval-numbero v val)))
      ((fresh (e v)
         (== `(symbol? ,e) expr)
         (eval-expro e env v)
         (eval-symbolo v val)))
      ((fresh (e v)
         (== `(car ,e) expr)
         (eval-expro e env v)
         (eval-caro v val)))
      ((fresh (e v)
         (== `(cdr ,e) expr)
         (eval-expro e env v)
         (eval-cdro v val)))      
      ((fresh (e1 e2 v1 v2)
         (== `(cons ,e1 ,e2) expr)
         (== `(,v1 . ,v2) val)
         (eval-expro e1 env v1)
         (eval-expro e2 env v2)))
      ((fresh (e1 e2 e3 v1)
         (== `(if ,e1 ,e2 ,e3) expr)
         (eval-expro e1 env v1)
         (eval-ifo env v1 e2 e3 val)))
      ((fresh (e1 e2 f v)
         (== `(,e1 ,e2) expr)
         (eval-expro e1 env f)
         (eval-expro e2 env v)
         (apply-expro f v val))))))

(define eval-nullo
  (lambda (v val)
    (conde
      ((== '() v)  (== #t val))
      ((== #f v)   (== #f val))
      ((== #t v)   (== #f val))
      ((numbero v) (== #f val))
      ((symbolo v) (== #f val))
      ((fresh (v1 v2)
         (== `(,v1 . ,v2) v)
         (=/= 'closure v1)
         (=/= 'N v1)
         (== #f val)))
      ((fresh (x env body)
         (== `(closure (,x) ,env ,body) v)
         (== #f val)))
      ((fresh (n)
         (== `(N ,n) v)
         (== `(N (NNull? ,n)) val))))))

(define eval-pairo
  (lambda (v val)
    (conde
      ((fresh (v1 v2)
         (== `(,v1 . ,v2) v)
         (=/= 'closure v1)
         (=/= 'N v1)
         (== #t val)))
      ((== '() v)  (== #f val))
      ((== #f v)   (== #f val))
      ((== #t v)   (== #f val))
      ((numbero v) (== #f val))
      ((symbolo v) (== #f val))
      ((fresh (x env body)
         (== `(closure (,x) ,env ,body) v)
         (== #f val)))
      ((fresh (n)
         (== `(N ,n) v)
         (== `(N (NPair? ,n)) val))))))

(define eval-numbero
  (lambda (v val)
    (conde
      ((numbero v) (== #t val))
      ((== #f v)   (== #f val))
      ((== #t v)   (== #f val))
      ((== '() v)  (== #f val))
      ((symbolo v) (== #f val))
      ((fresh (v1 v2)
         (== `(,v1 . ,v2) v)
         (=/= 'closure v1)
         (=/= 'N v1)
         (== #f val)))
      ((fresh (x env body)
         (== `(closure (,x) ,env ,body) v)
         (== #f val)))
      ((fresh (n)
         (== `(N ,n) v)
         (== `(N (NNumber? ,n)) val))))))

(define eval-symbolo
  (lambda (v val)
    (conde
      ((symbolo v) (== #t val))
      ((== #f v)   (== #f val))
      ((== #t v)   (== #f val))
      ((== '() v)  (== #f val))      
      ((numbero v) (== #f val))
      ((fresh (v1 v2)
         (== `(,v1 . ,v2) v)
         (=/= 'closure v1)
         (=/= 'N v1)
         (== #f val)))
      ((fresh (x env body)
         (== `(closure (,x) ,env ,body) v)
         (== #f val)))
      ((fresh (n)
         (== `(N ,n) v)
         (== `(N (NSymbol? ,n)) val))))))


(define eval-caro
  (lambda (v val)
    (conde
      ((fresh (v1 v2)
         (== `(,v1 . ,v2) v)
         (== v1 val)
         (=/= 'closure v1)
         (=/= 'N v1)))
      ((fresh (n)
         (== `(N ,n) v)
         (== `(N (NCar ,n)) val))))))

(define eval-cdro
  (lambda (v val)
    (conde
      ((fresh (v1 v2)
         (== `(,v1 . ,v2) v)
         (== v2 val)
         (=/= 'closure v1)
         (=/= 'N v1)))
      ((fresh (n)
         (== `(N ,n) v)
         (== `(N (NCdr ,n)) val))))))


(define eval-ifo
  (lambda (env v1 e2 e3 val)
    (conde
      ((== #t v1)
       (eval-expro e2 env val))
      ((== #f v1)
       (eval-expro e3 env val))
      ((fresh (n1 v2 v3)
         (== `(N ,n1) v1)
         (== `(N (NIf ,n1 ,v2 ,v3)) val)
         (eval-expro e2 env v2)
         (eval-expro e3 env v3))))))


(define apply-expro
  (lambda (f v val)
    (conde
      ((fresh (n)
         (== `(N ,n) f)
         (== `(N (NApp ,n ,v)) val)))
      ((fresh (x body env)
         (== `(closure (,x) ,body ,env) f)
         (symbolo x)
         (eval-expro body `((,x . ,v) . ,env) val))))))

;; Fast and simple fresho definition (written with Michael Ballantyne)
;; Rather than compute a renamed variable, we just describe the constraints.
(define fresho
  (lambda (xs x^)
    (fresh ()
      (symbolo x^)
      (absento x^ xs))))

(define quoted-or-self-quotingo
  (lambda (expr datum)
    (conde
      ((== #f expr) (== expr datum))
      ((== #t expr) (== expr datum))
      ((numbero expr) (== expr datum))
      ((== `(quote ,datum) expr)))))

(define not-quoted-and-not-self-quotingo
  (lambda (expr)
    (conde
      ((symbolo expr))
      ((== '() expr))
      ((fresh (a d)
         (== `(,a . ,d) expr)
         (=/= 'quote a))))))

(define uneval-valueo
  (lambda (xs v expr)
    (conde
      ((== #f v) (== #f expr))
      ((== #t v) (== #t expr))
      ((numbero v) (== v expr))
      ((== '() v) (== '(quote ()) expr))
      ((symbolo v)
       (== `(quote ,v) expr)
       (=/= 'closure v)
       (=/= 'N v))
      ((fresh (n)
         (== `(N ,n) v)
         (uneval-neutralo xs n expr)))
      ((fresh (x body env x^ body^ bv)
         (== `(closure (,x) ,body ,env) v)
         (== `(lambda (,x^) ,body^) expr)
         (symbolo x)
         (symbolo x^)
         (fresho xs x^)
         (eval-expro body `((,x . (N (NVar ,x^))) . ,env) bv)
         (uneval-valueo `(,x^ . ,xs) bv body^)))
      ((fresh (v1 v2 e1 e2)
         (== `(,v1 . ,v2) v)
         (=/= 'closure v1)
         (=/= 'N v1)
         (absento 'closure expr)
         (absento 'N expr)
         (conde
           ((fresh (d1 d2)
              (== `(quote (,d1 . ,d2)) expr)
              (quoted-or-self-quotingo e1 d1)
              (quoted-or-self-quotingo e2 d2)))
           ((== `(cons ,e1 ,e2) expr)
            (conde
              ((not-quoted-and-not-self-quotingo e1))
              ((fresh (d1)
                 (quoted-or-self-quotingo e1 d1)
                 (not-quoted-and-not-self-quotingo e2))))))
         (uneval-valueo xs v1 e1)
         (uneval-valueo xs v2 e2))))))

(define uneval-neutralo
  (lambda (xs n expr)
    (conde
      ((== `(NVar ,expr) n)
       (symbolo expr))
      ((fresh (n1 e1)
         (== `(NNull? ,n1) n)
         (== `(null? ,e1) expr)
         (uneval-neutralo xs n1 e1)))
      ((fresh (n1 e1)
         (== `(NPair? ,n1) n)
         (== `(pair? ,e1) expr)
         (uneval-neutralo xs n1 e1)))
      ((fresh (n1 e1)
         (== `(NNumber? ,n1) n)
         (== `(number? ,e1) expr)
         (uneval-neutralo xs n1 e1)))
      ((fresh (n1 e1)
         (== `(NSymbol? ,n1) n)
         (== `(symbol? ,e1) expr)
         (uneval-neutralo xs n1 e1)))
      ((fresh (n1 e1)
         (== `(NCar ,n1) n)
         (== `(car ,e1) expr)
         (uneval-neutralo xs n1 e1)))
      ((fresh (n1 e1)
         (== `(NCdr ,n1) n)
         (== `(cdr ,e1) expr)
         (uneval-neutralo xs n1 e1)))
      ((fresh (n^ v ne ve)
         (== `(NApp ,n^ ,v) n)
         (== `(,ne ,ve) expr)
         (uneval-neutralo xs n^ ne)
         (uneval-valueo xs v ve)))
      ((fresh (n1 v2 v3 e1 e2 e3)
         (== `(NIf ,n1 ,v2 ,v3) n)
         (== `(if ,e1 ,e2 ,e3) expr)
         (uneval-neutralo xs n1 e1)
         (uneval-valueo xs v2 e2)
         (uneval-valueo xs v3 e3))))))

(define nfo
  (lambda (t env expr)
    (fresh (v)
      (eval-expro t env v)
      (uneval-valueo '() v expr))))

(define main
  (lambda ()
    (run* (result)
      (fresh (id_ const_)
        (eval-expro '(lambda (x) x) '() id_)
        (eval-expro '(lambda (x) (lambda (y) x)) '() const_)
        (eval-expro '(const id) `((id . ,id_) (const . ,const_)) result)))))


(test "main"
  (main)
  '((closure (y) x ((x . (closure (x) x ()))))))

;; nf [] (Lam "x" (App (Lam "y" (App (Var "x") (Var "y"))) (Lam "x" (Var "x"))))
;; =>
;; Lam "x" (App (Var "x") (Lam "x'" (Var "x'")))
(test "nf-0"
  (run* (expr)
    (nfo '(lambda (x) ((lambda (y) (x y)) (lambda (x) x))) '() expr))
  '(((lambda (_.0) (_.0 (lambda (_.1) _.1)))
     (=/= ((_.0 _.1)))
     (sym _.0 _.1))))


